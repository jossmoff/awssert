plugins {
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.21.0'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.joss'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    junitVersion = "5.10.1"
    awsSdkVersion = "2.21.19"
}

repositories {
    mavenCentral()
}


dependencies {
    implementation("org.assertj:assertj-core:3.24.2")
    implementation("software.amazon.awssdk:sfn:${awsSdkVersion}")
    implementation("software.amazon.awssdk:s3:${awsSdkVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitVersion}")
    testImplementation("org.mockito:mockito-core:5.6.0")
}



spotless {
    java {
        googleJavaFormat()
    }
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.joss'
            artifactId = 'awssert'
            from components.java
            pom {
                name = 'awssert'
                description = 'Description of this repo.'
                url = 'https://github.com/jossmoff/awssert'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'jossmoff'
                        name = 'Joss Moffatt'
                        email = 'josshmoffatt@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:jossmoff/awssert.git'
                    developerConnection = 'scm:git@github.com:jossmoff/awssert.git'
                    url = 'https://github.com/jossmoff/awssert'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("sonatypeUsername")
                password = project.findProperty("sonatypePassword")
            }
        }
    }
}

signing {
    def signingKey = project.findProperty("signingKey")
    def signingPassword = project.findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
